#  SPDX-License-Identifier: Apache-2.0 WITH LLVM-exception
?start: series
?series: (spaced_term "," | graph)* term
?term: spaced_term | graph
?spaced_term: connections | alias | member
connections: vertex (connector vertex)+
?connector: "-" vertex? "-" -> edge
    | "-" vertex? "->" -> right_arrow
    | "<-" vertex? "-" -> left_arrow
?alias: vertex ":=" vertex
?vertex: graph | member
graph: [member] "{" series? _COMMA_RIGHT_BRACKET
# We need to ensure there is
# no space between dots and units.
# This issue (https://github.com/lark-parser/lark/discussions/934)
# provides a solution: concatenate terminals
# together in a new terminal
member: MEMBER
MEMBER: DOTS (IDENT | STRING | _HASH NUMBER) ELEMENT*
    | UNIT ELEMENT*
ELEMENT: _DOT+ (IDENT| STRING)? | _DOT* _HASH NUMBER?
UNIT: IDENT | STRING | NUMBER
DOTS: _DOT+
_DOT: "."
_HASH: "#"
# We use the same technique for adding trailing commas
# for any term inside a graph
_COMMA_RIGHT_BRACKET: ","? WHITE_SPACE* "}" ","?

%import .terminal.IDENT -> IDENT
%import .terminal.NUMBER -> NUMBER
%import .terminal.STRING -> STRING
%import .terminal.WHITE_SPACE -> WHITE_SPACE
%ignore WHITE_SPACE